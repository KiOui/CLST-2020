# Praat script for transcribing speech recordings. This script lists the
# wav files in its directory and lets the user go through them one-by-one
# providing an empty textgrid file with 1 tier for transcription of the 
# perceived speech.
#
# Author: Mario Ganzeboom
# Last modified: November 4, 2016 

firstIteration = 1
strings = Create Strings as file list: "list", "*.wav"
numberOfFiles = Get number of strings

# First determine the file number to start with
startfile = 0
fileNrToStart = 0
doStop = 0
for startfile to numberOfFiles
    selectObject: strings
    name$ = Get string: startfile

    fileNameLength = length(name$)
    filename$ = left$ (name$, fileNameLength-4)
    tgfilename$ = filename$ + "_man-trans"
    tempfile$ = filename$ + "_man-trans.temp"
    targetfile$ = tgfilename$ + ".tg"
    if fileReadable(tempfile$)
        fileNrToStart = startfile
	startfile = numberOfFiles
    else
        if not fileReadable(targetfile$)
            fileNrToStart = startfile
	    startfile = numberOfFiles
        else
            if startfile >= numberOfFiles
                startfile = numberOfFiles
            endif
        endif
    endif
endfor

if fileNrToStart == 0
    # All files in this directory have been processed nothing to be done.
    beginPause: "Alles verwerkt"
        comment: "Alle bestanden zijn al verwerkt. Er is niets meer te doen."
        comment: "Klik op 'Sluiten' of 'Stoppen' om dit programma af te sluiten."
    clicked = endPause: "Sluiten", "Stoppen", 1, 2
    if clicked == 1 or clicked == 2
        exit
    endif
endif

for ifile to numberOfFiles
    ifile = fileNrToStart
    selectObject: strings
    name$ = Get string: ifile

    fileNameLength = length(name$)
    filename$ = left$ (name$, fileNameLength-4)

    tgfilename$ = filename$
    wavfilename$ = replace$ (filename$, "@", "_", 0)
    wavfile$ = filename$ + ".wav"
    startfile$ = replace$ (tgfilename$, "@", "_", 0) + "_man-trans"
    tempfile$ = tgfilename$ + "_man-trans.temp"
    targetfile$ = tgfilename$ + "_man-trans.tg"

    #if not fileReadable(targetfile$)
		isContinuing = 0
		isTextGridCreated = 0
		if fileReadable(tempfile$)
			isContinuing = 1
			Read from file... 'tempfile$'
		else
			if fileReadable(targetfile$)
				Read from file... 'targetfile$'
			else
				#exit File 'startfile$' or 'tempfile$' missing !
				Open long sound file... 'wavfile$'
				select LongSound 'wavfilename$'
				dur = Get total duration
				tier_name$ = "Words"
				Create TextGrid... 0 dur 'tier_name$'
				isTextGridCreated = 1
			endif
		endif
		clicked = 1
		windowTitle$ = ""
		if firstIteration == 1
			if isContinuing == 0
				windowTitle$ = "Starten met annotatie"
			else
				windowTitle$ = "Verder annoteren"
			endif
			beginPause: windowTitle$
				again$ = ""
				if isContinuing == 0
					comment: "Je gaat zometeen starten met het annoteren van het volgende bestand: "
					comment: wavfile$
				else
					comment: "Je gaat zometeen het volgende bestand verder annoteren: "
					comment: tempfile$
				endif
				comment: "1. Klik op 'Volgende' om het bestand te openen in het annotatiescherm."
				comment: "2. Switch naar dat scherm met de ALT+TAB toetsencombinatie."
				comment: "3. Annoteer het."
				comment: "4. Sluit het annotatiescherm en klik weer op 'Volgende' voor het volgende"
				comment: "bestand."
				comment: ""
				comment: "Ben je gestart met het annoteren van een bestand en wil je later verder?"
				comment: "1. Sla het op met de extensie '.temp' (i.p.v. de standaard '.tg') via"
				comment: "(File -> Save Textgrid as text file)."
				comment: "2. Sluit het annotatiescherm en klik op 'Stoppen'."
			clicked = endPause: "Volgende", "Stoppen", 1, 2
			if clicked == 2
				exit
			endif
		endif
		
		tge_name$ = ""
		if isTextGridCreated == 1
			tge_name$ = "Words"
		else
			Open long sound file... 'wavfile$'
			tge_name$ = startfile$
		endif

		select LongSound 'wavfilename$'
		plus TextGrid 'tge_name$'
		Edit
		#runSystem: "/Applications/cliclick kd:alt kp:f2 ku:alt"
		#runSystem: "/Applications/cliclick kd:alt kp:f1 ku:alt"
		editor: "TextGrid " + tge_name$
			Show all
			Move cursor to: 0
		endeditor
		beginPause: "Volgend bestand?"
			comment: "Klaar voor de volgende opname? Klik op 'Volgende'."
            comment: "Wil je terug naar de vorige opname? Klik op 'Vorige' (niet mogelijk bij de 1e opname)."
			comment: "Klik op 'Stoppen' om later verder te gaan met het annoteren."
		clicked = endPause: "Volgende", "Vorige", "Stoppen", 1, 2
		if clicked == 3
			exit
		endif
		if clicked == 1
			select TextGrid 'tge_name$'
			Write to short text file... 'targetfile$'
			select LongSound 'wavfilename$'
			plus TextGrid 'tge_name$'
			Remove
			filedelete 'tempfile$'
                        fileNrToStart = fileNrToStart + 1
		else
			if clicked == 2
				select TextGrid 'tge_name$'
				Write to short text file... 'targetfile$'
				select LongSound 'wavfilename$'
				plus TextGrid 'tge_name$'
				Remove
				filedelete 'tempfile$'
				fileNrToStart = fileNrToStart - 1
			endif
		endif
		
		# First iteration is complete. Set to 0 to not display the introduction
		# message anymore
		firstIteration = 0
	#endif
endfor

beginPause: "Alles verwerkt"
    comment: "Heel goed! Alle opnames in deze map zijn nu verwerkt."
    comment: "Klik op 'Sluiten' of 'Stoppen' om het programma af te sluiten."
endPause: "Sluiten", "Stoppen", 1, 2
